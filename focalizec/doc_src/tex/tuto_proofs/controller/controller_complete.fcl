open "basics" ;;

(** Type of signals colors. *)
type color_t = | C_green | C_orange | C_red ;;


(** Type of states the automaton can be. Simply named with letters corresponding
    to the colors of signal 1, 2 and 3. *)
type state_t = | S_grr | S_orr | S_rgr | S_ror | S_rrg| S_rro ;;


(** Species embedding the automaton controlling the signals colors changes. *)
species Controller =
  (* Need to encode tuples as nested pairs because of limitations of Coq and
     Zenon. *)
  representation = (state_t * (color_t * (color_t * color_t))) ;
  let init : Self = (S_grr, (C_green, (C_red, C_red))) ;

  (** Extractors of "tuples" components. *)
  let get_s (x : Self) = match x with | (a, _) -> a ;
  let get_s1 (x : Self) = match x with | (_, a) -> match a with | (b, _) -> b ;
  let get_s2 (x : Self) =
    match x with | (_, a) -> match a with | (_, b) -> match b with | (c, _) -> c ;
  let get_s3 (x :Self) =
    match x with | (_, a) -> match a with | (_, b) -> match b with | (_, c) -> c ;


  (** Main controller function: automaton's 1 step run. *)
  let run_step (state : Self) : Self =
    match get_s (state) with
    | S_grr -> (S_orr, (C_orange, (C_red, C_red)))
    | S_orr -> (S_rgr, (C_red, (C_green, C_red)))
    | S_rgr -> (S_ror, (C_red, (C_orange, C_red)))
    | S_ror -> (S_rrg, (C_red, (C_red, C_green)))
    | S_rrg -> (S_rro, (C_red, (C_red, C_orange)))
    | S_rro -> (S_grr, (C_green, (C_red, C_red))) ;


  (** Lemma stating that s1 and s2 are never green together. It's 1/3 of the
      final property stating that no signals are green at the same time. *)
  theorem never_s1_s2_green :
    all s r : Self,
    r = run_step (s) ->
    ~ (get_s1 (r) = C_green /\ get_s2 (r) = C_green)
  proof =
  <1>1 assume s : Self, r : Self,
       hypothesis h1 : r = run_step (s),
       prove ~ (get_s1 (r) = C_green /\ get_s2 (r) = C_green)

       (* Proof by cases on values of the "automaton state" of s.
          For each case, we will prove that one of the 2 signal at least is
          not green. *)
       <2>1 hypothesis h2: get_s (s) = S_grr,
            prove ~ (get_s1 (r) = C_green)
            (* To prove the signal s1 is not green, we prove it is orange. *)
            <3>1 prove get_s1 (r) = C_orange
                 by hypothesis h1, h2
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       (* Same proof kind for all the cases of automaton state. *)
       <2>2 hypothesis h3: get_s (s) = S_orr,
            prove ~ (get_s1 (r) = C_green)
            (* To prove the signal s1 is not green, we prove it is red. *)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h3
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       (* Same proof kind for all the cases of automaton state. *)
       <2>3 hypothesis h4: get_s (s) = S_rgr,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h4
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>4 hypothesis h5: get_s (s) = S_ror,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h5
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>5 hypothesis h6: get_s (s) = S_rrg,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h6
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>6 hypothesis h7: get_s (s) = S_rro,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_red
                 by hypothesis h1, h7
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>7 qed by
            step <2>1, <2>2, <2>3, <2>4, <2>5, <2>6
            definition of run_step
            hypothesis h1
            type state_t
  <1>2 conclude ;


  (* Same proof kind than for never_s1_s2_green. *)
  theorem never_s1_s3_green :
    all s r : Self,
    r = run_step (s) ->
    ~ (get_s1 (r) = C_green /\ get_s3 (r) = C_green)
  proof =
  <1>1 assume s : Self, r : Self,
       hypothesis h1 : r = run_step (s),
       prove ~ (get_s1 (r) = C_green /\ get_s3 (r) = C_green)

       (* Proof by cases on values of the "automaton state" of s. *)

       <2>1 hypothesis h2: get_s (s) = S_grr,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_orange
                 by hypothesis h1, h2
                    definition of get_s, get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>2 hypothesis h3: get_s (s) = S_orr,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h3
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>3 hypothesis h4: get_s (s) = S_rgr,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h4
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>4 hypothesis h5: get_s (s) = S_ror,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h5
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>5 hypothesis h6: get_s (s) = S_rrg,
            prove ~ (get_s1 (r) = C_green)
            <3>1 prove get_s1 (r) = C_red
                 by hypothesis h1, h6
                    definition of get_s1, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>6 hypothesis h7: get_s (s) = S_rro,
            prove ~ (get_s3 (r) = C_green)
            <3>1 prove get_s3 (r) = C_red
                 by hypothesis h1, h7
                    definition of get_s3, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>7 qed by
            step <2>1, <2>2, <2>3, <2>4, <2>5, <2>6
            definition of run_step
            hypothesis h1
            type state_t
  <1>2 conclude ;


  (* Same proof kind than for never_s1_s2_green. *)
  theorem never_s2_s3_green :
    all s r : Self,
    r = run_step (s) ->
    ~ (get_s2 (r) = C_green /\ get_s3 (r) = C_green)
  proof =
  <1>1 assume s : Self, r : Self,
       hypothesis h1 : r = run_step (s),
       prove ~ (get_s2 (r) = C_green /\ get_s3 (r) = C_green)

       (* Proof by cases on values of the "automaton state" of s. *)

       <2>1 hypothesis h2: get_s (s) = S_grr,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_red
                 by hypothesis h1, h2
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>2 hypothesis h3: get_s (s) = S_orr,
            prove ~ (get_s3 (r) = C_green)
            <3>1 prove get_s3 (r) = C_red
                 by hypothesis h1, h3
                    definition of get_s3, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>3 hypothesis h4: get_s (s) = S_rgr,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_orange
                 by hypothesis h1, h4
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>4 hypothesis h5: get_s (s) = S_ror,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_red
                 by hypothesis h1, h5
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>5 hypothesis h6: get_s (s) = S_rrg,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_red
                 by hypothesis h1, h6
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>6 hypothesis h7: get_s (s) = S_rro,
            prove ~ (get_s2 (r) = C_green)
            <3>1 prove get_s2 (r) = C_red
                 by hypothesis h1, h7
                    definition of get_s2, run_step
                    type state_t, color_t
            <3>2 qed by step <3>1 type color_t

       <2>7 qed by
            step <2>1, <2>2, <2>3, <2>4, <2>5, <2>6
            definition of run_step
            hypothesis h1
            type state_t
  <1>2 conclude ;


  (** The complete theorem stating that no signals are green at the same time. *)
  theorem never_2_green :
    all s r : Self,
    r = run_step (s) ->
    ~ ((get_s1 (r) = C_green /\ get_s2 (r) = C_green) \/
       (get_s1 (r) = C_green /\ get_s3 (r) = C_green) \/
       (get_s2 (r) = C_green /\ get_s3 (r) = C_green))
  proof =
    by property never_s1_s2_green, never_s1_s3_green, never_s2_s3_green ;

end ;;
