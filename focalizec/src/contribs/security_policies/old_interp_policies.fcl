
open "sets";;
open "finite_sets";;
open "utils";;
open "p_policies";;


(*
species Targeted_P_policy (A is Target, C is Configuration, F is Finite_parts(A)) =

  (** Relation specifying secure targets according to configurations *)
  inherit P_policy(A, C);

  signature is_secure_target_of: C -> F -> prop;
 
  (* spec : is_secure_target_of(c,ens) ssi all t in ens secure(t,c) *)

end;;
*)

species Interpreted_Target(A is Target) =
  inherit Target;
  signature interpretation : A -> Self;
end;;




species Identity_Interpreted_Target(A is Target) =
  inherit Interpreted_Target(A);
  representation = A;
  let interpretation(a) = a;
  let element = A!element;
  let equal(a, b) = A!equal(a, b);
  proof of equal_reflexive = by property A!equal_reflexive
                                definition of equal;
  proof of equal_symmetric = by property A!equal_symmetric
                                definition of equal;
  proof of equal_transitive = by property A!equal_transitive
                                definition of equal;
end;;


species Interpreted_configurations(C is Configuration) =
  inherit Configuration;
  signature interpretation : C -> Self;
end;;

species Interpreted_configurations_by_parts
        (A is Target, C is Configuration, D is Interpreted_Target(A),
         FD is Finite_parts(D)) =
  inherit Interpreted_configurations(C);

  signature as_part : Self -> FD;
 
end;;


species Interpreted_P_policy 
        (A is Target, C is Configuration, 
         P is P_policy(A, C), 
         IT is Interpreted_Target(A), 
         FIT is Finite_parts(IT),
         IC is Interpreted_configurations_by_parts(A, C, IT, FIT)) =

  inherit P_policy(IT, IC);

  let secure(it, ic) = FIT!belongs(it, IC!as_part(ic));

  property compat_interpretations :  all c: C, all a: A, 
   FIT!belongs(IT!interpretation(a), IC!as_part(IC!interpretation(c))) 
   <->
   P!secure(a, c);
    
end;;


species Extensional_p_policy(A is Target, C is Configuration, P is P_policy(A, C),
                             AI is Identity_Interpreted_Target(A),
                             FAI is Finite_parts(AI),
                             IC is Interpreted_configurations_by_parts(A, C, AI, FAI)) =
  inherit Interpreted_P_policy(A, C, P, AI, FAI, IC); 
end;;
