#NAME dk_int.
Nat : Type := dk_nat.Nat.
O : Nat := dk_nat.O.
S : Nat -> Nat := dk_nat.S.

B : Type := cc.eT dk_bool.bool.

(; integers are defined by couples of naturals seen as their difference modulo the rule
   S n - S m --> n - m
 ;)
int : cc.uT.
I : Type := cc.eT int.
Int := I.
(; the only constructor of Int, make n m builds the integer n - m ;)
make : Nat -> Nat -> I.
(;
thanks to this rule, intergers reduce to one of the three following normal form :
 make O O which is 0
or
 make (S n) O which is n+1
or
 make O (S n) which is -n-1
;)
[n : Nat, m : Nat] make (dk_nat.S n) (dk_nat.S m) --> make n m.

from_nat : Nat -> I.
[ n : Nat ] from_nat n --> make n O.

nat_abs : I -> Nat.
[n : Nat] nat_abs (make n dk_nat.O) --> n
[m : Nat] nat_abs (make dk_nat.O m) --> m.

(; n - m <= p - q iff n + q <= m + p ;)
leq : I -> I -> B.
[n : Nat, m : Nat, p : Nat, q : Nat]
    leq (make n m) (make p q)
      --> dk_nat.leq (dk_nat.plus n q) (dk_nat.plus m p).

(; n - m < p - q iff n + q < m + p ;)
lt : I -> I -> B.
[n : Nat, m : Nat, p : Nat, q : Nat]
    lt (make n m) (make p q)
      --> dk_nat.lt (dk_nat.plus n q) (dk_nat.plus m p).

geq : I -> I -> B.
[ i : I, j : I ] geq i j --> leq j i.

gt : I -> I -> B.
[ i : I, j : I ] gt i j --> lt j i.

eq : I -> I -> B.
[ i : I, j : I ] eq i j --> dk_bool.and (leq i j) (geq i j).

(; (n - m) + (p - q) = (n + p) - (m + q) ;)
plus : I -> I -> I.
[n : Nat,
 m : Nat,
 p : Nat,
 q : Nat]
    plus (make n m) (make p q)
      --> make (dk_nat.plus n p) (dk_nat.plus m q).

opp : I -> I.
[n : Nat, m : Nat]
    opp (make n m) --> make m n.

sub : I -> I -> I.
[i : I, j : I]
    sub i j --> plus i (opp j).

mult : I -> I -> I.
[n : Nat, m : Nat, p : Nat, q : Nat]
    mult (make n m) (make p q)
      --> make
         (dk_nat.plus (dk_nat.mult n p) (dk_nat.mult m q))
         (dk_nat.plus (dk_nat.mult n q) (dk_nat.mult m p)).

max : I -> I -> I.
[ m : I, n : I ]
    max m n --> dk_bool.ite int (leq m n) n m.

min : I -> I -> I.
[ m : I, n : I ]
    min m n --> dk_bool.ite int (leq m n) m n.

abs : I -> I.
[ i : I ] abs i --> make (nat_abs i) O.

mod : I -> I -> I.
[n : Nat, m : Nat, p : Nat]
    mod (make m n) (make p dk_nat.O)
      --> make (dk_nat.mod m p) (dk_nat.mod n p)
[n : Nat, m : Nat, p : Nat]
    mod (make m n) (make dk_nat.O p)
      --> make (dk_nat.mod m p) (dk_nat.mod n p).

quo : I -> I -> I.
[m : Nat, p : Nat]
    quo (make m dk_nat.O) (make p dk_nat.O)
      --> make (dk_nat.quo m p) O
[m : Nat, p : Nat]
    quo (make dk_nat.O m) (make dk_nat.O p)
      --> make (dk_nat.quo m p) O
[m : Nat, p : Nat]
    quo (make dk_nat.O m) (make p dk_nat.O)
      --> make O (dk_nat.quo m p)
[m : Nat, p : Nat]
    quo (make m dk_nat.O) (make dk_nat.O p)
      --> make O (dk_nat.quo m p).
